
buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.9.6"
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'com.jfrog.artifactory'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'

description = """Spring Cloud CredStash"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-starter-parent:2.0.9.RELEASE'
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Finchley.SR4'
    }
}

dependencies {

    // Spring Boot AutoConfigure and Spring Cloud
    compileOnly 'org.springframework.boot:spring-boot-autoconfigure'
    compileOnly 'org.springframework.cloud:spring-cloud-context:2.0.4.RELEASE'

    // AWS KMS, DynamoDB
    compile 'com.amazonaws:aws-java-sdk-kms:1.11.605'
    compile 'com.amazonaws:aws-java-sdk-dynamodb:1.11.605'

    // BouncyCastle Crypto
    compile 'org.bouncycastle:bcprov-jdk15on:1.62'

    // Test
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile "junit:junit"
    testCompile 'org.hamcrest:hamcrest-all:1.3'

    // "production" dependencies
    testCompile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
    }
    testCompile "org.springframework.boot:spring-boot-starter-jetty"
    testCompile 'org.springframework.cloud:spring-cloud-context'
    testCompile 'org.bouncycastle:bcprov-jdk15on:1.62'
    testCompile 'com.amazonaws:aws-java-sdk-kms'
    testCompile 'com.amazonaws:aws-java-sdk-dynamodb'

}

jar {
    baseName = rootProject.name
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

artifactory {
    contextUrl = "${artifactory_url}/artifactory"
    publish {
        repository {
            repoKey = 'catalytic-java-libs' // The Artifactory repository key to publish to
            username = "${artifactory_user}" // The publisher user name
            password = "${artifactory_password}" // The publisher password
        }
        defaults {
            // Reference to Gradle publications defined in the build script.
            // This is how we tell the Artifactory Plugin which artifacts should be
            // published to Artifactory.
            publications('mavenJava')
            publishArtifacts = true
            // Properties to be attached to the published artifacts.
            properties = ['source.category': 'open', 'owner' : 'catalytic']
            // Publish generated POM files to Artifactory (true by default)
            publishPom = true
        }
    }
}
